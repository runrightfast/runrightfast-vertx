buildscript {
    repositories {
        jcenter()
    }

    dependencies {
        classpath 'se.transmode.gradle:gradle-docker:1.2'
        classpath 'com.google.protobuf:protobuf-gradle-plugin:0.7.0'
    }
}

apply plugin: 'application'
apply plugin: 'docker'
apply plugin: 'com.google.protobuf'

protobuf {
    protoc {
        // Download from repositories
        artifact = 'com.google.protobuf:protoc:3.0.0-beta-1'
    }
    
    generatedFilesBaseDir = "$buildDir/generated/source/proto"
}

sourceSets {
    main {
        java {
            srcDirs = ['src/main/java',"$buildDir/generated/source/proto/main/java"]
        }
        resources {
            srcDirs = ['src/main/resources','src/main/proto']
        }
    }
    test {
        java {
            srcDirs = ['src/test/java',"$buildDir/generated/source/proto/test/java"]
        }
        resources {
            srcDirs = ['src/test/resources','src/test/proto']
        }
    }
}

mainClassName = 'co.runrightfast.vertx.demo.VertxApp'

applicationDefaultJvmArgs = [    
    // enable remote debugging
//    "-Xdebug",
//    "-Xrunjdwp:server=y,transport=dt_socket,address=4000,suspend=n",
    
    // JMX
    "-Dcom.sun.management.jmxremote.authenticate=false",
    "-Dcom.sun.management.jmxremote.ssl=false",
    "-Dcom.sun.management.jmxremote.port=7410",
    
//    "-Dapp.log.dir=${buildDir}/app/logs",   
    "-Drunrightfast.app.version=${version}",
    
//    "-Dconfig.resource=application_local.conf"
    
//    "-Dvertx.cluster.public.host=192.168.0.100",
//    "-Dvertx.cluster.public.port=60558"
]

dependencies {
   compile project(':runrightfast-vertx-orientdb')
}


docker {
    maintainer = 'Alfio Zappala <alfio.a.z.2@gamil.com>'
    // OpenJDK
    //baseImage = 'java:8u45-jre'
    
    // Oracle JDKs
    baseImage = 'andreptb/oracle-java:8'
    // baseImage = 'isuper/java-oracle:jre_8'
}

task appDocker(type: Docker) {
    dependsOn installDist
    project.group = "runrightfast"
    applicationName = "${project.name}"
    entryPoint(["/runrightfast/apps/${project.name}/bin/${project.name}"])
    // JMX port
    exposePort 7410
    // Remote debug port
    exposePort 4000    
    addFile("${buildDir}/install/${project.name}", "/runrightfast/apps/${project.name}")
}
